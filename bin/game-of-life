#!/usr/bin/env perl6
use v6;

use Game::Life::Board :board;
use Game::Life;
use Game::Life::Renderer;
use Game::Life::Player;
use Game::Life::Player::Basic;
use Game::Life::Player::BasicIndexed;
use Game::Life::Player::DivideAndConquer;

sub initialize-renderer(Bool :$graphics, Promise :$done --> Supplier) {
    my $render = Supplier.new;
    start {
        react {
            whenever $render.Supply -> (Int $i, Board $board) {
                if $graphics {
                    if render-graphics($board) {
                        $done.keep;
                    }
                }
                else {
                    render-text($board);

                    say "--- $i " ~ '-' x (3 max ($board.width - $i.Str.chars - 5));
                }
            }
        }
    }

    $render;
}

sub initialize-game-of-life(Bool :$parallel, Bool :$indexed, Str :$start-state) {
    my $player = do if $indexed {
        Game::Life::Player::BasicIndexed.new;
    }
    elsif $parallel {
        Game::Life::Player::DivideAndConquer.new;
    }
    else {
        Game::Life::Player::Basic.new;
    }

    my $board = make-board($start-state, :immutable);
    my $game = Game::Life.new(:$player, :$board);

    supply {
        for 0..* -> $i {
            emit( ($i, $game.board) );
            $game.next-turn;
        }
    }
}

sub initialize-start-state(Str :$start-file) {
    with $start-file {
        $start-file.IO.slurp;
    }
    else {
        q:to/END_OF_DEFAULT_START_STATE/;
                                X
                              X X
                    XX      XX            XX
                   X   X    XX            XX
        XX        X     X   XX
        XX        X   X XX    X X
                  X     X       X
                   X   X
                    XX
        END_OF_DEFAULT_START_STATE
    }
}

sub MAIN(
    Int :$steps = -1,
    Bool :$parallel = False,
    Bool :$indexed = False,
    Bool :$graphics = False,
    Str :$start-file,
) {
    my $start-state = initialize-start-state(:$start-file);

	initialize-graphics if $graphics;

    my Promise $done .= new;
    my Supplier $render = initialize-renderer(:$graphics, :$done);
    my Supply $turn = initialize-game-of-life(:$start-state, :$parallel, :$indexed);

    react {
        whenever $turn -> (Int $i, Board $board) {
            $render.emit: ($i, $board);

            if $steps >= 0 && $i >= $steps {
                $done.keep;
            }
        }

        whenever signal(SIGINT, SIGQUIT) {
            $done.keep;
        }

        whenever $done {
            done;
        }
    }
}
